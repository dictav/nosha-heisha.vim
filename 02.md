# Vimを再入門

## vimrcの書き方

何はともあれ `:help` を使って調べることをお勧めします。また、`:help user-manual@ja`
ネットにある情報は誤っていることがあります。

そんな中でも参照するべきネットの情報源として以下を挙げておきます

* [vim-jp » Vimのユーザーと開発者を結ぶコミュニティサイト](http://vim-jp.org)
* [thinca さんの永遠に未完成]( http://d.hatena.ne.jp/thinca/)

### vimrc の読み込み

vimrcは以下の順に検索され、最初に見つかったものだけが読み込まれます。

		Unix		$HOME/.vimrc、$HOME/.vim/vimrc
		OS/2		$HOME/.vimrc、$HOME/vimfiles/vimrc、
				$VIM/.vimrc (または _vimrc)
		MS-Windows	$HOME/_vimrc、$HOME/vimfiles/vimrc、
				$VIM/_vimrc
		Amiga		s:.vimrc、home:.vimrc、home:vimfiles:vimrc、
				$VIM/.vimrc

起動時に `-u` を使うことで任意のファイルをvimrcとして読み込めますが `$MYVIMRC` は設定されないことに注意してください。また `-u NORC` でvimrcを読み込まなくなり、`-u NONE` では plugin も読み込まなくなります。(※この辺は次回)

#### お題

通常起動の時と `-u` 付けて起動した時の `:echo $MYVIMRC` の結果の比較

### 最初に設定すること

``` vim
set nocompatible
set ignorecase
set smartcase
filetype plugin indent on
syntax enable
```

* nocompatible: Vi互換を切りVimの便利な機能を使えるようにする
* ignorecase/smartcase: 検索時に大文字小文字をいい感じに扱う
* filetype: ファイルタイプ毎の plugin/indent を有効にする
* syntax: シンタックスハイライトを有効にする

### オプションをセットする

vimには300個くらいオプションがあり、自分好みに挙動を変更することができます。
どのようなリストがあるかは `:help option-list` すると見ることができます。
オプションをセットするためには `set <option[=param]>` を使います。
`set` は一度に複数の値を設定することができますが、スペースが区切り文字になっていることに注意してください

`set` のオプション値は完全に文字列なので変数などを当てることができません。変数を使いたいようば場合は `exe` コマンドを使って `set` コマンドを実行するか、`let` 指示子を使って直接オプション値を書き換える必要があります。

```
let s:tmp_dir = "/path/to/tmp_dir"
set directory=s:tmp_dir
" => directory の値は "s:tmp_dir"

exe "set directory=".s:tmp_dir
" => directory の値は "/path/to/tmp_dir" 

let &directory = s:tmp_dir
" => directory の値は "/path/to/tmp_dir" 
```

`$HOME` のように `$` prefix を使うと環境変数にアクセスもできます。値が設定されていない場合には "" として評価されるので、オプションを設定するときには役立つかもしれません。

#### お題 (10分)

essential.vim を改良して、以下のようになるように設定してください。

1. 左に行番号を表示する
2. タブ幅を3にする
3. `d` や `c` などのコマンドを最下部に表示されるようにする
4. laststatusを設定しステータスバーを常に表示するようにする

```
1 def hoge
2    print('hoge')
3 end
```

### キーマッピング

vim ではキーマッピングを自由に変更することができます。
やりすぎるとノーマルvimを触れない体になるので注意が必要です。

#### map と noremap

キーマッピングの設定するコマンドには `map` と `noremap` の２つがあります。
`map` が再帰的にマッピングを繰り返すのに対して `noremap` はそれを行ないません。

```
map j gj
map gj j " 再起!
```

```
noremap j gj
noremap gj j " j と gj を置き換えた
```

`:map` 空打ちすると現在のキーマップ一覧を得ることができます。

#### モード別キーマッピング

ノーマルモード/挿入モード/コマンドランモード/ビジュアルモードではそれぞれ別々にキーマッピングすることができます。

`imap`/`inoremap` のように prefix をつけて指定します。

| prefix | mode           |
|--------|----------------|
| n      | ノーマルモード |
| i      | 挿入           |
| c      | コマンドライン |
| v      | ビジュアル     |

キーマッピングを行うときは影響範囲に十分注意してくだい

また、`map`/`noremap` には `<silent>`/`<buffer>` のような幾つかのオプションがあります。
詳しくは `:help map-arguments` を参照してください。


#### 空のマップを作る

`:map <F8> <Nop>` を作ることができます。これと `showcmd` を利用して複数キーマップを繋げて便利に使うことができます。

```
set showcmd

nnoremap [echo] <Nop>
nmap <Space>e [echo]
nnoremap [echo]h :echo "hello"<CR>
nnoremap [echo]w :echo "world"<CR>
```

上のように essential.vim に追加し、 `<Space>eh`、`<Space>ew` と打ってみてください。

#### 関数呼び出し

`<SID>` を使うことでスクリプト内の関数を呼び出すことができます。
(`s:` などのvimscriptのスコープについては次回)

```
function! s:hello_world()
  echo "hello, world"
endfunction

noremap <C-f> :call <SID>hello_world()<CR>
```


#### お題(5分)

Ctrl+f を押すことで 1,2,3,... と表示するキーマッピングを作成する


### オートコマンド

一定の条件下で自動的にコマンドを実行したいことがあります。
以下は vim filetype のファイルを開いた時にタブ幅を 2 にするような設定です。

```
autocmd FileType vim setlocal tabstop=2 shiftwidth=2 softtabstop=0
```

#### グループ

オートコマンドはグループ毎に設定します。
シンプルに以下のように書いた場合はカレントグループを指します。

```
autocmd FileType vim setlocal tabstop=2 shiftwidth=2 softtabstop=0
```

オートコマンドの削除はこのグループ毎に行います。 `autocmd!` ではカレントグループの全てのオートコマンドを削除します。

```
augroup MyGroup " グループを MyGroup へ変更
  autocmd FileType vim echom 'hello,'
  autocmd FileType vim echom 'vim!'
augroup END " グループをスクリプトのカレントグループに変更

autocmd! MyGroup " MyGroup のオートコマンドを全て削除
```

#### オートコマンドの定義

オートコマンドはスクリプトの読み込み毎に定義されてしまうため、同じ定義がなんども重複して定義されてしまうことが起こります。
そこでグループ定義と `autocmd!` を使って毎回リセットしてから定義します。

```
augroup MyGroup
  autocmd!
  autocmd FileType vim echom 'hello,'
  autocmd FileType vim echom 'vim!'
augroup END
```

#### お題（10分)

次のような動作をするオートコマンドを設定する

1. C のファイルを開いた時に `:echo 'hello, C!'` してタブ幅を4にする
2. Python のファイルを開いた時に `:echo 'hello, Python!` してタブ幅を2にする
3. Rubyのファイルを開いた時に `:echo 'hello, Ruby!` してタブ幅を3にする

